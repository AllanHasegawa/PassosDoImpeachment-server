buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.1"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

group 'com.hasegawa.di'
version '1.0-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'com.hasegawa.di.server.MainApplication'

shadowJar {
    mergeServiceFiles()
}

repositories {
    mavenCentral()
}

project.ext {
    dropwizardVersion = '1.0.0-rc1'
    kotlinVersion = '1.0.1'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
    compile "io.dropwizard:dropwizard-client:$dropwizardVersion"
    compile "io.dropwizard:dropwizard-jdbi:$dropwizardVersion"
    compile "io.dropwizard:dropwizard-migrations:$dropwizardVersion"
    compile "io.dropwizard:dropwizard-auth:$dropwizardVersion"
    compile "io.dropwizard:dropwizard-assets:$dropwizardVersion"
    compile "org.postgresql:postgresql:9.4.1208"
    compile 'de.thomaskrille:dropwizard-template-config:1.2.0'
    compile 'org.mindrot:jbcrypt:0.3m'
    compile 'io.reactivex:rxjava:1.1.2'
}

String configFile() {
    if (project.hasProperty("production")) {
        return "prod-configuration.yml"
    } else {
        return "configuration.yml"
    }
}

run {
    args 'server', configFile()
}

// for the db cmd calls reference, see:
// http://www.dropwizard.io/0.9.2/docs/manual/migrations.html
task dbStatus(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args "db", "status", configFile()
}

task dbMigrate(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args "db", "migrate", configFile()
}

task dbDropAll(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args "db", "drop-all", "--confirm-delete-everything", configFile()
}

